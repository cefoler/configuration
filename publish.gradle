apply plugin: "signing"
apply plugin: "maven-publish"

ext {
    issueUrl = "$projectUrl/issues"

    connectionUrl = "scm:git:git://$projectGit"
    developerUrl = "scm:git:git@$projectGit"
}

signing {
    sign configurations.archives
}

publishing {
    repositories {
        maven {
            if (project.hasProperty("snapshotUrl") && project.hasProperty("releaseUrl")) {
                url = projectVersion.endsWith('-SNAPSHOT') ? snapshotUrl : releaseUrl

                if (project.hasProperty("ossrhUsername") && project.hasProperty("ossrhPassword")) {
                    credentials {
                        username = ossrhUsername
                        password = ossrhPassword
                    }
                }
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            groupId projectGroup
            artifactId projectArtifact

            version projectVersion

            pom {
                name = projectArtifact
                description = projectDescription

                url = projectUrl

                issueManagement {
                    system = projectSystem
                    url = issueUrl
                }

                licenses {
                    license {
                        name = licenseName
                        url = licenseUrl
                    }
                }

                developers {
                    developer {
                        id = "DeserRC"
                        name = "Diego Cruz"
                        email = "deserrc@gmail.com"
                        organization = incorporationName
                        organizationUrl = incorporationUrl
                    }
                }

                scm {
                    url = projectUrl
                    connection = connectionUrl
                    developerConnection = developerUrl
                }
            }

            from components.java
        }
    }
}

artifacts {
    archives javadocJar, sourcesJar
}
